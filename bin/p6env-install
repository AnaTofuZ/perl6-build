#!/usr/bin/env perl
#
# Summary: Install a Perl6 version using perl6-build
#
# Usage: p6env install <rakudo-star-version> [-- [configure options]]
#        p6env install <rakudo-commitish>    [-- [configure options]]
#        p6env install -l|--list
#        p6env install -L|--list-all
#
#  -l, --list      list available versions (latest 20 versions)
#  -L, --list-all  list all available versions
#      --as NAME   install perl6 as NAME
#      --jvm       install perl6 with jvm backend
#      --version   show version for perl6-build
#
#  Example:
#   # Install rakudo-star-2018.04
#   $ p6env install rakudo-star-2018.04
#
#   # Install perl6 from rakudo git repository with 2018.06 tag
#   $ p6env install 2018.06
#
#   # Install perl6 from rakudo git repository with HEAD
#   $ p6env install HEAD
#
#   # Install rakudo-star-2018.04 with jvm backend
#   $ p6env install --jvm rakudo-star-2018.04
#
#   # Install perl6 from rakudo git repository with 2018.06 tag and custom configure options
#   $ p6env install 2018.06 --as 2018.06-custom -- --backends moar --with-nqp /path/to/bin/nqp
#
use strict;
use warnings;
use Cwd ();
use FindBin;
use Getopt::Long ();
use File::Spec;

my @configure_option;
my ($index) = grep { $ARGV[$_] eq '--' } 0..$#ARGV;
if (defined $index) {
    (undef, @configure_option) = splice @ARGV, $index, @ARGV - $index;
}

Getopt::Long::Configure(qw(default no_auto_abbrev no_ignore_case));
Getopt::Long::GetOptions
    "l|list"      => \my $list,
    "L|list-all"  => \my $list_all,
    "h|help"      => \my $show_help,
    "as=s"        => \my $as,
    "complete"    => \my $complete,
    "jvm"         => \my $jvm,
    "version"     => \my $show_version,
or exit 1;

# Provide p6env completions
if ($complete) {
    print "$_\n" for qw(--list --list-all --as --version --jvm);
    exit;
}

if ($show_help) {
    open my $fh, "<", $0 or die;
    my $first = <$fh>;
    while (<$fh>) {
        s/^#// and print and next;
        exit;
    }
}
my $perl6_build = File::Spec->catdir($FindBin::Bin, "perl6-build");
my $base_dir = Cwd::abs_path(File::Spec->catdir($FindBin::Bin, ".."));

if ($show_version) {
    exec $^X, $perl6_build, "--version";
    exit 255;
}
if ($list || $list_all) {
    my $ok = !system $^X, $perl6_build, $list ? "-l" : "-L";
    system $^X, $perl6_build, "--ensure-latest-version", $base_dir if !$ok;
    exit($ok ? 0 : 1);
}


my $version = shift or die "Invalid argument. Try `p6env install --help`.\n";
my $root = $ENV{P6ENV_ROOT} || File::Spec->catdir($ENV{HOME}, ".p6env");
if (!$as) {
    if ($version eq 'HEAD') {
        $as = '{describe}';
    } else {
        $as = $version;
    }
    $as = "$as-jvm" if $jvm;
}
my $prefix = File::Spec->catdir($root, "versions", $as);
my @cmd = (
    $^X,
    $perl6_build,
    "--workdir", $root,
    $jvm ? '--jvm' : (),
    $version,
    $prefix,
    (@configure_option ? ("--", @configure_option) : ()),
);

my $ok = !system @cmd;
if ($ok) {
    system "p6env", "rehash";
} else {
    system $^X, $perl6_build, "--ensure-latest-version", $base_dir;
}
exit($ok ? 0 : 1);
